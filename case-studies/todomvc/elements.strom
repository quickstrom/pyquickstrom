//-------------------------------------------------
// DOM elements
//-------------------------------------------------

let ~selectedFilter = 
  let element = first(`.todoapp .filters a.selected`);
  if element != null && element.visible { element.textContent };

let ~items =
  let listItems = `.todo-list li`;
  for [index, [itemVisible, editing], [text, labelVisible], checked, input] in zipAll([ 
    range(0, length(listItems) - 1),
    (for item in listItems { [item.css.display != "none", contains("editing", item.classList)] }),  
    (for label in `.todo-list li label` { [label.textContent, label.visible] }),
    (for cb in `.todo-list li input[type=checkbox]` { cb.checked }),
    (for e in `.todo-list li .edit` { { pendingText: e.value, active: e.active } })
  ]) {
    if itemVisible && (labelVisible || editing) {
      { index: index, text: text, checked: checked, editing: editing, editInput: input }
    }
  };

let ~lastItemText = 
  let item = last(items);
  if item != null { item.text };

let ~numItems = length(items);

let ~numUnchecked = length(for i in items { i when not i.checked });

let ~numChecked = length(for i in items { i when i.checked });

let ~itemsInEditMode = for item in items { item when item.editing };

let ~itemInEditMode = first(itemsInEditMode);

let ~numInEditMode = length(itemsInEditMode);

let ~isInEditMode = numInEditMode >= 1;

let ~newTodoInput = 
  first(for e in `.todoapp .new-todo` {
    { pendingText: e.value, active: e.active }
  });

let ~todoCount =
    let strong = first(for e in `.todoapp .todo-count strong` { e.textContent when e.visible });
    let t = first(split(" ", trim(strong))) when strong != null;
    if t != null && t != "" { parseInt(t) };

let ~availableFilters = for f in `.todoapp .filters a` { f.textContent };

let ~toggleAllChecked = first(for c in `.todoapp #toggle-all` { c.checked }) ?? false;
